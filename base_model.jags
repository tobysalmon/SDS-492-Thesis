
model {
  # Likelihood for all time points
  # NA values will be automatically predicted by JAGS
  for(i in 1:tmax) {
    flu[i] ~ dpois(lambda1[i])
    rsv[i] ~ dpois(lambda2[i])
    covid[i] ~ dpois(lambda3[i])
    ili[i] ~ dpois(lambda4[i])
  }

  # Log-linear model for rates
  for(i in 1:tmax) {
    lambda1[i] <- exp(phi[i,1])
    lambda2[i] <- exp(phi[i,2])
    lambda3[i] <- exp(phi[i,3])
    lambda4[i] <- exp(phi[i,4])
  }

  # Priors - AR(1) process for each disease
  for(k in 1:4) {
    mu0[k] ~ dnorm(0, 1e-4)
    prec.phi[k] ~ dgamma(0.01, 0.01)
    rho1[k] ~ dunif(0, 1)

    # First time point - stationary distribution
    # Stationary precision = prec.phi * (1 - rho^2)
    phi[1,k] ~ dnorm(mu0[k], prec.phi[k] * (1 - pow(rho1[k], 2)))

    # AR(1) evolution for subsequent time points
    for(i in 2:tmax) {
      phi[i,k] ~ dnorm(mu0[k] + rho1[k] * (phi[i-1,k] - mu0[k]), prec.phi[k])
    }
  }
}

