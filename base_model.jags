
model {
  # Likelihood - Negative Binomial for all time points
  # NA values will be automatically predicted by JAGS
  for(i in 1:tmax) {
    flu[i]   ~ dnegbin(p1[i], r[1])
    rsv[i]   ~ dnegbin(p2[i], r[2])
    covid[i] ~ dnegbin(p3[i], r[3])
    ili[i]   ~ dnegbin(p4[i], r[4])

    # Reparameterize: p = r/(r+lambda) so mean = lambda, var = lambda + lambda^2/r
    p1[i] <- r[1] / (r[1] + lambda1[i])
    p2[i] <- r[2] / (r[2] + lambda2[i])
    p3[i] <- r[3] / (r[3] + lambda3[i])
    p4[i] <- r[4] / (r[4] + lambda4[i])

    lambda1[i] <- exp(phi[i,1])
    lambda2[i] <- exp(phi[i,2])
    lambda3[i] <- exp(phi[i,3])
    lambda4[i] <- exp(phi[i,4])
  }

  # Priors - Seasonal AR(1) process for each disease
  for(k in 1:4) {
    r[k] ~ dunif(0, 250)

    mu0[k] ~ dnorm(0, 1e-4)
    beta_sin[k] ~ dnorm(0, 0.1)
    beta_cos[k] ~ dnorm(0, 0.1)
    prec.phi[k] ~ dgamma(0.01, 0.01)
    rho1[k] ~ dunif(0, 1)

    # Seasonal mean at time 1
    mu_t[1,k] <- mu0[k] + beta_sin[k] * sin52[1] + beta_cos[k] * cos52[1]

    # First time point - stationary distribution
    phi[1,k] ~ dnorm(mu_t[1,k], prec.phi[k] * (1 - pow(rho1[k], 2)))

    # AR(1) with seasonal mean for subsequent time points
    for(i in 2:tmax) {
      mu_t[i,k] <- mu0[k] + beta_sin[k] * sin52[i] + beta_cos[k] * cos52[i]
      phi[i,k] ~ dnorm(mu_t[i,k] + rho1[k] * (phi[i-1,k] - mu_t[i-1,k]),
                        prec.phi[k])
    }
  }
}

